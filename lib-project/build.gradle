plugins {
  id 'groovy'
}

group = "com.kazurayam"
version = "0.5.0-SNAPSHOT"

ext {
  groovyVersion = '2.4.2'
  javadiffutilsVersion = '4.12'
  javadiffreportVersion = '0.1.0-SNAPSHOT'
  AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
}

configurations {
  generateDocs
  katalonDrivers
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  katalonDrivers "io.github.java-diff-utils:java-diff-utils:${javadiffutilsVersion}"
  katalonDrivers "com.kazurayam:java-diff-report:${javadiffreportVersion}"

  implementation fileTree(dir: "./Drivers", include: ['*.jar'])
  implementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
  implementation fileTree(dir: "/Applications/Katalon Studio.app/Contents/Eclipse/plugins", include: ['*.jar'])
  
  generateDocs "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

sourceSets {
  main {
    groovy {
      srcDirs = ["Keywords"]
      // excludes = ["CustomKeywords.groovy", "Temp*.groovy", "internal/GlobalVariable.groovy"]
    }
  }
  test {
    groovy {
      srcDirs = ["Include/scripts/groovy"]
    }
  }
}


task groovydoc(type: Groovydoc, overwrite:true) {
  group = 'com.kazurayam'
  source = sourceSets.main.groovy
  classpath = configurations.generateDocs
  groovyClasspath = project.configurations.generateDocs
  include 'com/kazurayam/ks/*'
  exclude '**/*Test.groovy'
}

task publishGroovydoc(type: Copy) {
  from 'build/docs/groovydoc'
  into '../docs/api'
}

groovydoc.finalizedBy publishGroovydoc

task groovydocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from groovydoc
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

build.finalizedBy groovydocJar, sourcesJar

task drivers {
  doFirst {
    delete fileTree("./Drivers") {
      include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
    }
  }
  doLast {
    copy {
      from configurations.katalonDrivers
      into("Drivers") {
        include "**/java-diff-utils*.jar"
        include "**/java-diff-report*.jar"
      }
      eachFile({ f -> println f.getName()})
      rename({ s -> "${AUTO_IMPORTED_JAR_PREFIX}${s}"})
    }
  }
}
